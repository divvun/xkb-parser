file = { SOI ~ (definition)* ~ EOI }
    definition = { what? ~ symbols ~ NEWLINE* }

    what = { how ~ (ident)+ ~ NEWLINE+ }
        how = { default_partial | hidden_partial | partial }
            default_partial = { "default" ~ "partial" }
            hidden_partial = { "hidden" ~ "partial" }
            partial = { "partial" }

    symbols = { "xkb_symbols" ~ string ~ "{" ~ NEWLINE* ~ (symbol)* ~ NEWLINE* ~ "}" ~ ";"? }
        symbol = { ( include | name | key | modifier_map ) ~ NEWLINE* }
            include = { "include" ~ string }
            name = { "name" ~ "[" ~ ident ~ "]" ~ "=" ~ string ~ ";"? }
            key = { "key" ~ ident2 ~ "{" ~ NEWLINE* ~ key_values ~ NEWLINE* ~ "}" ~ ";"? }
                key_values = { key_names | key_defs }
                    key_names = { "[" ~ (key_name)* ~ "]" }
                        key_name = _{ ident ~ ","? }
                    key_defs = { key_def+ }
                        key_def = { (type_def | symbol_def) ~ ","? ~ NEWLINE* }
                            type_def = { "type" ~ "=" ~ string }
                            symbol_def = { "symbols" ~ "[" ~ ident ~ "]" ~ "=" ~ key_names ~ ";"? }
            modifier_map = { "modifier_map" ~ ident ~ "{" ~ ( modifier ~ ","? )* ~ "}" ~ ";"? }
                modifier = { ident2 | ident }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE+ }
ident = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }
ident2 = { "<" ~ ident ~ ">" }

string = _{ "\"" ~ string_content ~ "\"" }
string_content = @{ char* }
    char = {
        !("\"" | "\\") ~ ANY
        | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
        | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    }
