file = { SOI ~ (definition)* ~ EOI }
    definition = { what? ~ symbols ~ NEWLINE* }

    what = { how? ~ (ident)* ~ NEWLINE+ }
        how = { default_partial | hidden_partial | partial }
            default_partial = { "default" ~ "partial" }
            hidden_partial = { "hidden" ~ "partial" }
            partial = { "partial" }

    symbols = { "xkb_symbols" ~ string ~ NEWLINE* ~ "{" ~ NEWLINE* ~ (symbol)* ~ NEWLINE* ~ "}" ~ ";"? }
        symbol = { ( include | name | key | key_type | virtual_modifiers | modifier_map ) ~ NEWLINE* }
            include = { "include" ~ string }
            name = { "name" ~ group ~ "=" ~ string ~ ";"? }
            key_type = { "key" ~ "." ~ "type" ~ group ~ "=" ~ string ~ ";"? }
            virtual_modifiers = { "virtual_modifiers" ~ (ident ~ ARRAY_SEP?)+ ~ ";"? }
            key = { "key" ~ ident2 ~ "{" ~ NEWLINE* ~ (key_value ~ ARRAY_SEP?)* ~ NEWLINE* ~ "}" ~ ";"? }
                key_value = { key_names | key_def }
                key_names = { "[" ~ (key_name)* ~ "]" }
                    key_name = _{ ident ~ ARRAY_SEP? }
                key_def = { (type_def | symbol_def | virtual_mods_def) ~ ARRAY_SEP? ~ NEWLINE* }
                    type_def = { "type" ~ group? ~ "=" ~ string }
                    symbol_def = { "symbols" ~ group ~ "=" ~ key_names ~ ";"? }
                    virtual_mods_def = { "virtualMods" ~ "=" ~ ident ~ ";"? }
            modifier_map = { "modifier_map" ~ ident ~ "{" ~ ( modifier ~ ARRAY_SEP? )* ~ "}" ~ ";"? }
                modifier = { ident2 | ident }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE+ }
ARRAY_SEP = _{ "," ~ "\n"? }
ident = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }
ident2 = { "<" ~ ident ~ ">" }
group = { "[" ~ ident ~ "]" }

string = _{ "\"" ~ string_content ~ "\"" }
string_content = @{ char* }
    char = {
        !("\"" | "\\") ~ ANY
        | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
        | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    }
