file = { SOI ~ (definition)* ~ EOI }
    definition = { what ~ symbols ~ NEWLINE* }

    what = { how ~ (ident)+ ~ NEWLINE+ }
        how = { default_partial | hidden_partial | partial }
            default_partial = { "default" ~ "partial" }
            hidden_partial = { "hidden" ~ "partial" }
            partial = { "partial" }

    symbols = { "xkb_symbols" ~ string ~ "{" ~ NEWLINE* ~ (symbol)* ~ NEWLINE* ~ "}" ~ ";"? }
        symbol = { ( include | name | key | modifier_map ) ~ NEWLINE* }
            include = { "include" ~ string }
            name = { "name" ~ "[" ~ ident ~ "]" ~ "=" ~ string ~ ";"? }
            key = { "key" ~ "<" ~ ident ~ ">" ~ "{" ~ "[" ~ (key_name)* ~ "]" ~ "}" ~ ";"? }
                key_name = { ident ~ ","? }
            modifier_map = { "modifier_map" ~ (!NEWLINE ~ ANY)* ~ NEWLINE+ }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE+ }
ident = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }

string = _{ "\"" ~ string_content ~ "\"" }
string_content = @{ char* }
    char = {
        !("\"" | "\\") ~ ANY
        | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
        | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    }
